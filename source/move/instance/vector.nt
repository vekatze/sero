import {
  core.box {axiom-T},
  core.control {grow-E},
  core.int.show {show-int},
  core.vector {push-back, vector},
  this.move.common.get {get-int64},
  this.move.common.put {put-int64},
  this.move.decode {decode},
  this.move.encode {encode},
  this.move.instance.int64 {int64-sero},
  this.rule.get-kit {get-kit},
  this.rule.put-kit {put-kit},
  this.rule.sero {Sero, sero},
}

inline _put<a>(m: sero(a)): (k: &put-kit, xs: &vector(a)) -> unit {
  function (k, xs) {
    let len = core.vector.length(xs) in
    put-int64(k, len);
    core.vector.for-N(xs, function (x, _) {
      let Sero of {put} = m in
      put(k, x)
    })
  }
}

inline _get<a>(m: sero(a)): (k: &get-kit) -> ?meta vector(a) {
  function (k) {
    try len = get-int64(k) in
    let Sero of {get} = m in
    let base: vector(a) = core.vector.make(len) in
    try item =
      grow-E(base, len, function (acc, _) {
        try value = get(k) in
        Right(push-back(acc, axiom-T(value)))
      })
    in
    Right(magic cast(vector(a), meta vector(a), item))
  }
}

inline vector-sero<a>(!m: sero(a)): sero(vector(a)) {
  Sero of {
    put = _put(!m),
    get = _get(!m),
  }
}

inline _int-vector: sero(vector(int)) {
  vector-sero(int64-sero)
}

define zen(): unit {
  pin val = core.vector.from-list([1, 2, -123, 1384910234]) in
  match decode(_int-vector, encode(_int-vector, val, 8)) {
  | Left(_) =>
    print("left\n")
  | Right(vs) =>
    print("int-vector\n");
    pin vs = vs in
    core.vector.for-N(vs, function (v, _) {
      printf("val: {}\n", [show-int(*v)])
    })
  }
}
