import {
  core.int.io {print-int-line},
  this.common.get {get-int8},
  this.common.put {put-int8},
  this.decode {decode},
  this.encode {encode},
  this.instance.bool {bool-sero},
  this.instance.int64 {int64-sero},
  this.get-kit {get-kit},
  this.put-kit {put-kit},
  this.sero {Sero, sero},
}

inline _put<a, b>(m1: sero(a), m2: sero(b)): (k: &put-kit, x-or-y: &either(a, b)) -> unit {
  function (k, x-or-y) {
    case x-or-y {
    | Left(x) =>
      let Sero of {put} = m1;
      put-int8(k, 0);
      put(k, x)
    | Right(y) =>
      let Sero of {put} = m2;
      put-int8(k, 1);
      put(k, y)
    }
  }
}

inline _get<a, b>(m1: sero(a), m2: sero(b)): (k: &get-kit) -> ?meta either(a, b) {
  function (k) {
    try tag = get-int8(k);
    if eq-int8(tag, 0) {
      let Sero of {get} = m1;
      try l = get(k);
      Right(box {
        letbox l' = l;
        Left(l')
      })
    } else-if eq-int8(tag, 1) {
      let Sero of {get} = m2;
      try r = get(k);
      Right(box {
        letbox r' = r;
        Right(r')
      })
    } else {
      none
    }
  }
}

inline either-sero<a, b>(!m1: sero(a), !m2: sero(b)): sero(either(a, b)) {
  Sero of {
    put := _put(!m1, !m2),
    get := _get(!m1, !m2),
  }
}

inline _either-int-bool: sero(either(int, bool)) {
  either-sero(int64-sero, bool-sero)
}

define zen(): unit {
  pin val = Left(-1234);
  match decode(_either-int-bool, encode(_either-int-bool, val, 8)) {
  | Left(_) =>
    print("error\n")
  | Right(x-or-y) =>
    match x-or-y {
    | Left(x) =>
      print("int: ");
      print-int-line(x);
    | Right(_) =>
      print("bool\n")
    }
  }
}
